@name Ranging Table Generator
@persist MVel Ang AMin AMax AStp Stp NStp Delay Drag TABLENAME:string G:vector2  Table:table



if(first()) {
    Crate = entity():isWeldedTo()
    Drag = Crate:acfDragCoef()
    
    if(!Drag) {
        hint("E2 must be placed on valid ammo crate",5)
        entity():soundPlay(2,1,"buttons/button10.wav")
        exit()
    }
    
    
    ####### EDIT ########
    
    TABLENAME = "default.txt"   #use this name with fileLoad(...), fileRead() and jsonDecode() to access the table
    
    AMin = 0                    #angle parameters
    AMax = 85
    AStp = 5                    #angle between entries

    MVel = Crate:acfMuzzleVel()
    NStp = 100                  #sim steps for arc
    Stp = 2*MVel*39.37/(600*NStp)
    Delay = 500                 #keep ops down a little


    #####################

    Ang = AMax
    G = vec2(0,-gravity())
    Table = table()
    
    timer("range", 0)
}



if(clk("range")) {  #Iterate angle
    local V = MVel*39.37*vec2(cos(Ang), sin(Ang))  
    local P = vec2(0,0)
    local T = 0
            
    for(J=1,NStp) {     #Iterate arc RK4    
         
        #local D = V:length()*Drag
                
        local A1 = G - Drag*V:length()*V
    
        local V2 = V + A1*Stp/2
        local A2 = G - Drag*V2:length()*V2
    
        local V3 = V + A2*Stp/2
        local A3 = G - Drag*V3:length()*V3
        
        local V4 = V + A3*Stp
        local A4 = G - Drag*V4:length()*V4
        
        P += (Stp/6)*(V + 2*(V2+V3) + V4)
        V += (Stp/6)*(A1 + 2*(A2 + A3) + A4)
        T += Stp

        if(P:y() < 0) {   #hits "ground"
            local TPast = P:y()/V:y() #time past hitting y=0
            Dist = P:x() -TPast*V:x() #netwon back to surface level       
            T -= TPast               
            
            Table:pushVector(vec(round(Dist),Ang,round(T,1)))
            break
        }
    }
    
    
    if(Ang > AMin) {
        timer("range",Delay)
    }
    else{
        entity():soundPlay(1,3,"garrysmod/content_downloaded.wav")
        hint("Ranging table stored as "+TABLENAME, 5)
        fileWrite(TABLENAME,jsonEncode(Table))
    }
    
    Ang = Ang - AStp
}
